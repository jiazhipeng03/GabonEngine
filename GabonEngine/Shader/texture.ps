////////////////////////////////////////////////////////////////////////////////
// Filename: texture.ps
////////////////////////////////////////////////////////////////////////////////


/////////////
// GLOBALS //
/////////////
Texture2D shaderTexture;
SamplerState SampleType
{
	Filter = ANISOTROPIC;
	MaxAnisotropy = 4;

	AddressU = WRAP;
	AddressV = WRAP;
};

cbuffer DirLightBufferType
{
	float4 ambientColor;	
	float4 diffuseColor;
	float4 specularColor;
	float3 lightDirection;
	float specularPower;
};

cbuffer GeneralBuffer
{
	float  gFogStart;
	float  gFogRange;
	bool gFogEnable;
	float padding;
	float4 gFogColor;
}
//////////////
// TYPEDEFS //
//////////////
struct PixelInputType
{
    float4 position : SV_POSITION;
    float2 tex : TEXCOORD0;
	float3 normal : NORMAL;
	float3 viewDir : TEXCOORD1;
	float clip : SV_ClipDistance0;
};


////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////
float4 simple(PixelInputType input) : SV_TARGET
{
	float4 textureColor;

    // Sample the pixel color from the texture using the sampler at this texture coordinate location.
    textureColor = shaderTexture.Sample(SampleType, input.tex);

    return textureColor;	
}
float4 main(PixelInputType input) : SV_TARGET
{
	float4 textureColor;
	float3 lightDir;
	float lightIntensity;
	float4 color;
	float4 specular;
	float3 reflectDir;
	
	specular = float4(0.0f, 0.0f, 0.0f, 0.0f);
	color = ambientColor;

    // Sample the pixel color from the texture using the sampler at this texture coordinate location.
    textureColor = shaderTexture.Sample(SampleType, input.tex);
	lightDir = -lightDirection;
	lightIntensity = saturate(dot(input.normal, lightDir));

	if(lightIntensity > 0.0f)
	{
		float3 viewDir = normalize(input.viewDir);
		color += diffuseColor * lightIntensity;
		reflectDir = normalize(2*lightIntensity*input.normal - lightDir);
		specular = pow(saturate(dot(reflectDir, viewDir)), specularPower);
	}
	
	color = color * textureColor;
	
	color = saturate(color + specular);

	if(gFogEnable)
	{
		float distToEye = length(input.viewDir);
		float fogLerp = saturate( (distToEye - gFogStart) / gFogRange ); 

		// Blend the fog color and the lit color.
		color = lerp(color, gFogColor, fogLerp);
	}
    return color;	
}
